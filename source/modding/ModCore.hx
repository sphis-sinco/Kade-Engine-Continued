package modding;

import flixel.FlxState;
import modding.events.simple.FocusEvent.FocusType;
import flixel.util.FlxSignal;
import modding.events.*;
import modding.events.bases.*;
import modding.events.basic.*;
import modding.events.simple.*;
import modding.scripting.*;
import flixel.FlxG;
#if FEATURE_MODCORE
import polymod.backends.OpenFLBackend;
import polymod.backends.PolymodAssets.PolymodAssetType;
import polymod.format.ParseRules;
import polymod.Polymod;
import polymod.fs.ZipFileSystem;
#end

/**
 * Okay now this is epic.
 */
class ModCore
{
	/**
	 * The current API version.
	 * Must be formatted in Semantic Versioning v2; <MAJOR>.<MINOR>.<PATCH>.
	 * 
	 * Remember to increment the major version if you make breaking changes to mods!
	 */
	static final API_VERSION = ">=0.2.0 <0.3.0";

	static final MOD_DIRECTORY = "mods";

	// Use SysZipFileSystem on native and MemoryZipFilesystem on web.
	public static var modFileSystem:Null<ZipFileSystem> = null;

	public static function initialize()
	{
		#if FEATURE_MODCORE
		Debug.logInfo("Initializing ModCore...");
		loadModsById(getModIds());
		#else
		Debug.logInfo("ModCore not initialized; not supported on this platform.");
		#end
	}

	#if FEATURE_MODCORE
	public static function loadModsById(ids:Array<String>)
	{
		Debug.logInfo('Attempting to load ${ids.length} mods...');
		var loadedModList = polymod.Polymod.init({
			// Root directory for all mods.
			modRoot: MOD_DIRECTORY,
			// The directories for one or more mods to load.
			dirs: ids,
			// Framework being used to load assets. We're using a CUSTOM one which extends the OpenFL one.
			framework: CUSTOM,
			// The current version of our API.
			apiVersionRule: API_VERSION,
			// Call this function any time an error occurs.
			errorCallback: onPolymodError,
			// Enforce semantic version patterns for each mod.
			// modVersions: null,
			// A map telling Polymod what the asset type is for unfamiliar file extensions.
			// extensionMap: [],

			frameworkParams: buildFrameworkParams(),

			// Use a custom backend so we can get a picture of what's going on,
			// or even override behavior ourselves.
			customBackend: ModCoreBackend,

			// List of filenames to ignore in mods. Use the default list to ignore the metadata file, etc.
			ignoredFiles: Polymod.getDefaultIgnoreList(),

			// Parsing rules for various data formats.
			parseRules: buildParseRules(),

			// Parse hxc files and register the scripted classes in them.
			useScriptedClasses: true,
			loadScriptsAsync: #if html5 true #else false #end,
		});

		Debug.logInfo('Mod loading complete. We loaded ${loadedModList.length} / ${ids.length} mods.');

		for (mod in loadedModList)
			Debug.logTrace('  * ${mod.title} v${mod.modVersion} [${mod.id}]');

		var fileList = Polymod.listModFiles("IMAGE");
		Debug.logInfo('Installed mods have replaced ${fileList.length} images.');
		for (item in fileList)
			Debug.logTrace('  * $item');

		fileList = Polymod.listModFiles("TEXT");
		Debug.logInfo('Installed mods have replaced ${fileList.length} text files.');
		for (item in fileList)
			Debug.logTrace('  * $item');

		fileList = Polymod.listModFiles("MUSIC");
		Debug.logInfo('Installed mods have replaced ${fileList.length} music files.');
		for (item in fileList)
			Debug.logTrace('  * $item');

		fileList = Polymod.listModFiles("SOUND");
		Debug.logInfo('Installed mods have replaced ${fileList.length} sound files.');
		for (item in fileList)
			Debug.logTrace('  * $item');
	}

	public static function buildFileSystem():polymod.fs.ZipFileSystem
	{
		polymod.Polymod.onError = onPolymodError;
		return new ZipFileSystem({
			modRoot: MOD_DIRECTORY,
			autoScan: true
		});
	}

	static function getModIds():Array<String>
	{
		Debug.logInfo('Scanning the mods folder...');
		var modMetadata = Polymod.scan({modRoot: MOD_DIRECTORY});
		Debug.logInfo('Found ${modMetadata.length} mods when scanning.');
		var modIds = [for (i in modMetadata) i.id];
		return modIds;
	}

	static function buildParseRules():polymod.format.ParseRules
	{
		var output = polymod.format.ParseRules.getDefault();
		// Ensure TXT files have merge support.
		output.addType("txt", TextFileFormat.LINES);
		// Ensure script files have merge support.
		output.addType('hscript', TextFileFormat.PLAINTEXT);
		output.addType('hxs', TextFileFormat.PLAINTEXT);
		output.addType('hxc', TextFileFormat.PLAINTEXT);
		output.addType('hx', TextFileFormat.PLAINTEXT);

		// You can specify the format of a specific file, with file extension.
		// output.addFile("data/introText.txt", TextFileFormat.LINES)
		return output;
	}

	static inline function buildFrameworkParams():polymod.FrameworkParams
	{
		return {
			assetLibraryPaths: [
				"default" => "./preload", // ./preload
				"sm" => "./sm",
				"songs" => "./songs",
				"shared" => "./",
				"tutorial" => "./tutorial",
				"week1" => "./week1",
				"week2" => "./week2",
				"week3" => "./week3",
				"week4" => "./week4",
				"week5" => "./week5",
				"week6" => "./week6"
			]
		}
	}

	static function onPolymodError(error:PolymodError):Void
	{
		// Perform an action based on the error code.
		switch (error.code)
		{
			// case "parse_mod_version":
			// case "parse_api_version":
			// case "parse_mod_api_version":
			// case "missing_mod":
			// case "missing_meta":
			// case "missing_icon":
			// case "version_conflict_mod":
			// case "version_conflict_api":
			// case "version_prerelease_api":
			// case "param_mod_version":
			// case "framework_autodetect":
			// case "framework_init":
			// case "undefined_custom_backend":
			// case "failed_create_backend":
			// case "merge_error":
			// case "append_error":
			default:
				// Log the message based on its severity.
				switch (error.severity)
				{
					case NOTICE:
						Debug.logInfo(error.message, null);
					case WARNING:
						Debug.logWarn(error.message, null);
					case ERROR:
						Debug.logError(error.message, null);
				}
		}
	}

	public static function forceReloadAssets():Void
	{
		if (modFileSystem == null)
			modFileSystem = buildFileSystem();

		// Forcibly clear scripts so that scripts can be edited.
		ScriptManager.destroyScripts();
		Polymod.clearScripts();

		scriptInit();

		loadModsById(getModIds());
		ScriptManager.loadScripts();

		if (FlxG.state != null)
			FlxG.resetState();
	}

	static function scriptInit()
	{
		var signals:Map<String, Array<Dynamic>> = [
			'focusGained' => [
				FlxG.signals.focusGained,
				function() ScriptManager.callEvent(script ->
				{
					script.onFocusChange(new FocusEvent(FocusType.GAINED, script, CoolUtil.getCurrentState()));
				})
			],
			'focusLost' => [
				FlxG.signals.focusLost,
				function() ScriptManager.callEvent(script ->
				{
					script.onFocusChange(new FocusEvent(FocusType.LOST, script, CoolUtil.getCurrentState()));
				})
			],

			'preStateSwitch' => [
				FlxG.signals.preStateSwitch,
				function() ScriptManager.callEvent(script ->
				{
					script.onStateSwitch(new SwitchStateEvent(PRE, script, CoolUtil.getCurrentState()));
				})
			],
			'postStateSwitch' => [
				FlxG.signals.postStateSwitch,
				function() ScriptManager.callEvent(script ->
				{
					script.onStateSwitch(new SwitchStateEvent(POST, script, CoolUtil.getCurrentState()));
				})
			],

			'preStateCreate' => [
				FlxG.signals.preStateCreate,
				function(state:FlxState) ScriptManager.callEvent(script ->
				{
					script.onCreate(new CreateEvent(PRE, script, CoolUtil.getCurrentState()));
				})
			],
			'postStateCreate' => [
				MusicBeatState.postStateCreate,
				function(state:FlxState) ScriptManager.callEvent(script ->
				{
					script.onCreate(new CreateEvent(POST, script, CoolUtil.getCurrentState()));
				})
			],

			'preUpdate' => [
				FlxG.signals.preUpdate,
				function() ScriptManager.callEvent(script ->
				{
					script.onUpdate(new UpdateEvent(PRE, script, CoolUtil.getCurrentState()));
				})
			],
			'postUpdate' => [
				FlxG.signals.postUpdate,
				function() ScriptManager.callEvent(script ->
				{
					script.onUpdate(new UpdateEvent(PRE, script, CoolUtil.getCurrentState()));
				})
			],
		];

		for (signalName => signalVariables in signals)
		{
			// var signalClass:FlxSignal = signalVariables[0];
			var signalClass:FlxTypedSignal<Any->Void> = signalVariables[0];

			if (!signalClass.has(_ -> signalVariables[1]))
				signalClass.add(_ -> signalVariables[1]);
		}
	}
	#end
}

#if FEATURE_MODCORE
class ModCoreBackend extends OpenFLBackend
{
	public function new()
	{
		super();
		Debug.logTrace('Initialized custom asset loader backend.');
	}

	public override function clearCache()
	{
		super.clearCache();
		Debug.logWarn('Custom asset cache has been cleared.');
	}

	public override function exists(id:String):Bool
	{
		Debug.logTrace('Call to ModCoreBackend: exists($id)');
		return super.exists(id);
	}

	public override function getBytes(id:String):lime.utils.Bytes
	{
		Debug.logTrace('Call to ModCoreBackend: getBytes($id)');
		return super.getBytes(id);
	}

	public override function getText(id:String):String
	{
		Debug.logTrace('Call to ModCoreBackend: getText($id)');
		return super.getText(id);
	}

	public override function list(type:PolymodAssetType = null):Array<String>
	{
		Debug.logTrace('Listing assets in custom asset cache ($type).');
		return super.list(type);
	}
}
#end
